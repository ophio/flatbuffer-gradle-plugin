buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
    }
}

apply plugin: "groovy"
apply plugin: "maven-publish"
apply plugin: "maven"
apply plugin: 'com.jfrog.bintray'

archivesBaseName = "flatbuffer-gradle-plugin"
group = 'in.co.ophio'
version = '0.1.0'

//publishing
def orgName = 'ragdroid'
def versionControlUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
def description = 'description'
def versionName = version

repositories {
    mavenCentral ()
}

compileGroovy {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

dependencies {
    compile gradleApi()
    compile 'com.android.tools.build:gradle:1.3.0'
}

//local publishing
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:///$buildDir/m2repo")
        }
    }
}

//bintray publishing
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version versionName
        }
    }
}

bintray {
    def bintrayPropsFile = file('bintray.properties')

    if (bintrayPropsFile.exists()) {
        def bintrayProps = new Properties()
        bintrayProps.load(new FileInputStream(bintrayPropsFile))

        def username = bintrayProps['userName']
        def apikey = bintrayProps['apiKey']
        user = username
        key = apikey
        publications = ['MyPublication']
        dryRun = false
        publish = true
        pkg {
            repo = 'maven'
            name = archivesBaseName
            userOrg = orgName
            licenses = ['Apache-2.0']
            vcsUrl = versionControlUrl
            version {
                name = versionName
                desc = description
                released  = new Date()
            }
        }
    } else {
        println 'please add bintray.properties file'
    }
}

